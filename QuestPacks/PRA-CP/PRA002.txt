Quest: PRA002
DisplayName: The Ratcatcher General
-- Message panels
QRC:

QuestorOffer:  [1000]
You there %hnr! You seem like a capable
adventurer. I am _questgiver_, they call
me The Ratcatcher General. I'm the best
Ratcatcher in %reg. Ask anyone. It pains me
to admit this, but I need assistance. There is a
plague of rats in %cn and I'm struggling to
deal with them. I need you to keep the rats under
control while I poison their nests. Will you
help? I will give you a sizable share of the
bounty. What say you, %pcf?

RefuseQuest:   [1001]
I don't blame you %pcf. Ratcatching is hard and
dirty work that often goes un-appreciated by
folk around here. I'll await another brave
adventurer.
Good travels my friend.

AcceptQuest:  [1002]
Good. We must make with haste for _rathouse1_.
We need to do this within =timer_ days or the
rats will overrun %cn.

QuestFail:  [1003]
It's too late. The rats are out of control.
Flee for the hills!

QuestComplete:  [1004]
A great service we did today, %pcf. A great
service!
Here is your share of the bounty. You earned
it. I may call upon you again if the vermin
dare threaten our town.

RumorsDuringQuest:  [1005]
Did you know _questgiver calls himself The Ratcatcher General? What a stupid title.

Message:  1020
That's it! The nest is dealt with. The rest of
the rats should be easy work, then we can go.
<ce>
I'll take the bag of bait back, otherwise you'll
have hungry _rats_ chasing you for days.

Message: 1021
This is the place, it stinks of _rats_ spoor!
<ce>
Take this bag of bait, it's a special mixture
of mine. Mostly rotting bones and dried fishguts
with some secret ingredients I add to give it
potency. Trade secrets I'm afraid, but it'll keep
the rats on you and, more importantly, off me
while I find their nest.
<ce>
They are likely to get mighty feisty once I start
messing with it, so be careful! And good luck!

Message: 1022
Ok. I've found the nest. Get ready, they'll get
ornery now!

Message: 1025
You've left _questgiver_ to %g3 fate. Without
your assistance %g is surely overcome by his
quarry.
<ce>
To make matters worst the bag of bait has burst
and covered you in it's contents. Rats will
follow you for hours.

Message: 1026
The stench must have mellowed somewhat now, as
rats no longer seem to be following you.

Message:  1030
%qdt:
_questgiver_, who refers to %g2self as
The Ratcatcher General, has employed me to help
with a rat plague in %cn.
I am to escort %g2 to _rathouse1_
in ___rathouse1_ and slay the rats while
%g deals with the nests.

Message: 1031
%qdt:
_questgiver_ has given me a reeking bag to use as bait to
draw the rats while %g looks for the nest.

Message: 1032
%qdt:
I fled _rathouse1_ and left _questgiver_
alone with an angry horde of vermin. Surely %g'll
be done for. In a cruel twist of fate, the bag of
bait has split and emptied it's contents on my
clothes so now I'm drawing rats to me.
<ce>
I'll have to kill or run from them until the
stink wears off, hopefully in a few hours.

QBN:
Item _reward_ gold

Person _questgiver_ group Questor male

Place _rathouse1_ local house1

Clock _timer_ 1.00:00 0 flag 17 range 0 2

Clock _nest_ 00:10 00:30

Clock _destroyed_ 00:10 00:30

Clock _plague_ 01:00 03:00

Foe _rats_ is 2 Giant_rat

Foe _morerats_ is 4 Giant_rat

Item _lure_ large_sack

--	Quest start-up:
	start timer _timer_
    hide npc _questgiver_
    add _questgiver_ face
	log 1030 step 0 
	place foe _rats_ at _rathouse1_ 
    pc at _rathouse1_ set _inside_

_inside_ task:
    get item _lure_ saying 1021
    have _lure_ set _working_
    drop _questgiver_ face
    log 1031 step 1
    start timer _nest_

_spawn_ task:
	when _inside_ and not _done_ and not _abandoned_
    create foe _rats_ every 1 minutes indefinitely with 70% success

_working_ task:

_abandoned_ task:

_coward_ task:
    when _working_ and not _inside_
    say 1025
    setvar _abandoned_
    stop timer _nest_
    stop timer _timer_
    log 1032 step 2 
    start timer _plague_
    create foe _rats_ every 6 minutes indefinitely with 50% success
    unset _inside_

_pcgetsgold_ task:
	when _done_ and not _inside_ and not _abandoned_
	give pc _reward_ 
	end quest 

_done_ task:
    when _destroyed_ and not _abandoned_
    unset _working_
    say 1020
    take _lure_ from pc
    add _questgiver_ face

_qgclicked_ task:
	clicked npc _questgiver_ 

_timer_ task:
	end quest saying 1003

_nest_ task:
    when _inside_ and not _done_ and not _abandoned_ and not _destroyed_
    create foe _morerats_ every 1 minutes indefinitely with 70% success msg 1022
    start timer _destroyed_

_destroyed_ task:

_plague_ task:
    end quest saying 1026

_clearclick_ task:
	when _qgclicked_ and not _done_
	clear _qgclicked_ _clearclick_ 