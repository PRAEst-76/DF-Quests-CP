Quest: PRA1002
DisplayName: The Ratcatcher General
-- Message panels
QRC:

QuestorOffer:  [1000]
"You there %hnr! You seem like a capable
adventurer. I am _questgiver_, they call
me The Ratcatcher General. I'm the best
Ratcatcher in %reg. Ask anyone. It pains me
to admit this, but I need assistance. There is a
plague of rats in %cn and I'm struggling to
deal with them. I need you to keep the rats under
control while I poison their nests in three buildings.
Will you help? I will give you a sizable share of
the bounty. What say you, %pcf?"

RefuseQuest:   [1001]
"I don't blame you %pcf. Ratcatching is hard and
dirty work that often goes un-appreciated by
folk around here. I'll await another brave
adventurer."
<ce>
"Good travels my friend."

AcceptQuest:  [1002]
"Good. We must make with haste for the first location,
_rathouse1_."
<ce>
"We need to clear them all within =timer_ days or the
rats will overrun %cn."

QuestFail:  [1003]
"It's too late. The rats are out of control.
Flee for the hills!"

QuestComplete:  [1004]
"A great service we did today, %pcf! A great
service!"
<ce>
"Here is your share of the bounty and have a drink on
me. You earned it. We made a good team. I may call upon
you again if that vermin dare threaten our lands."

RumorsDuringQuest:  [1005]
Did you know _questgiver_ calls himself The Ratcatcher General? What a stupid title.
<--->
People are saying __questgiver_ is a breedin' ground for rats. Well at least someone is gettin' some breedin'.

RumorsPostfailure:  [1006]
The old Ratcatcher died doing what he loved; killin' rats.
<--->
I heard the Ratcatcher's latest apprentice abandoned him to be eaten alive by the beasts.

RumorsPostsuccess:  [1007]
Haven't seen many rats lately.
<--->
If they sang songs about killing rats they'd sing 'em about _questgiver_ but they don't, 'cause that would be stupid.
<--->
Awful what happened to all the rats in ___questgiver_. Lovely creatures. Who would do such a thing?
<--->
That Ratcatcher is a legend in the world of rat catching.

Message: 1021
"This is the place, it stinks of _rats_ spoor!"
<ce>
"Take this bag of bait, it's a special mixture
of mine. Mostly rotting bones and dried fishguts
with some secret ingredients I add to give it
potency. Trade secrets I'm afraid, but it'll keep
the rats on you and, more importantly, off me
while I find their nest."
<ce>
"They are likely to get mighty feisty once I start
messing with it, so be careful! And good luck!"

Message: 1022
"Ok. I've found the nest. Get ready, they'll get
ornery now!"

Message: 1023
"Ok. Here we are. Here is the bait.
Kill the rats, I'll do the nest."

Message: 1024
"Right. Here's the bait. You know the drill.
We can get drunk after this."

Message:  1025
"That's it! The first nest is dealt with. The rest of
the rats should be easy work, then we can go onto the
next place."
<ce>
"Be swift."
<ce>
"I'll take the bag of bait back, otherwise you'll
have hungry _rats_ chasing you for days."

Message:  1035
"Ok. The second house we need to clear is _rathouse2_."

Message: 1026
"Right. Second batch of nests dealt with. I'll take the
bait back. Mop up any stragglers and then we need to
head for the last place."

Message:  1036
"Right, head for _rathouse3_. It's the last one.
Then we can hit the tavern."

Message:  1038
"%pcf! Where are you going?"

Message:  1039
"Don't run off like that. This is a delicate process!"

Message: 1027
"All done my friend. Any rats that are left just
don't know they are dead yet."

Message:  1037
"Well done %pcf! Let's go to _pub_ and get hammered!"

Message:  1028
The Ratcatcher sprints into the tavern like a foal into
a spring meadow. You should catch up with him before he
spends your share at the bar.

Message:  1029
"Cheers %pcf! Here's to a good days work!"

Message: 1030
You've left _questgiver_ to %g3 fate. Without
your assistance %g is surely overcome by his
quarry.
<ce>
To make matters worst the bag of bait has burst
and covered you in it's contents. Rats will
follow you for hours.

Message: 1031
The stench must have mellowed somewhat now, as
rats no longer seem to be following.

Message:  1032
The barman shouts, "Ratcatcher! Just the man. I was
about to send one of the lads to look for you. We seem
to have a little vermin problem. Sort 'em out and the
drinks are free."

Message:  1033
The Ratcacher General tosses you the bait bag, "%pcf!
You know what to do!"

Message:  1034
"Cushty! Nest done, %pcf. Now join me for the free drink!"

Message:  1100
%qdt:
_questgiver_, who refers to %g2self as
The Ratcatcher General, has employed me to help
with a rat plague in %cn.
I am to escort %g2 to three buildings here and
slay rats while %g looks for the nests.
<ce>
We have =timer_ days before the rats are out of
control.
<ce>
We start with _rathouse1_.

Message: 1101
%qdt:
At _rathouse1_, _questgiver_ has given
me a reeking bag to use as bait to draw the rats
while %g looks for the nest.

Message: 1102
The first house has been dealt with, now onto the
next, which is _rathouse2_.

Message: 1103
%qdt:
Onto the last place now, which he tells me is
_rathouse3_

Message:  1105
%qdt:
We have dealt with all the infested houses.
_questgiver_ wants us to go to _pub_
to relax.

Message: 1104
%qdt:
I fled and left _questgiver_
alone with an angry horde of vermin. Surely %g'll
be done for. In a cruel twist of fate, the bag of
bait has split and emptied it's contents on my
clothes so now I'm drawing rats to me.
<ce>
I'll have to kill or run from them until the
stink wears off, hopefully in a few hours.

QBN:
Item _reward_ gold

Person _questgiver_ group Questor male

Place _rathouse1_ local house
Place _rathouse2_ local house
Place _rathouse3_ local shop
Place _pub_ local tavern

Clock _timer_ 1.00:00 0 flag 17 range 0 2
Clock _nest1_ 00:15 00:30
Clock _nest2_ 00:10 00:20
Clock _nest3_ 00:05 00:10
Clock _tavnest_ 00:05 00:10
Clock _destroyed1_ 00:05 00:10
Clock _destroyed2_ 00:10 00:20
Clock _destroyed3_ 00:15 00:30
Clock _destroyedtav_ 00:10 00:20
Clock _leftrc_ 00:02
Clock _plague_ 01:00 03:00

Foe _rats_ is 2 Giant_rat
Foe _morerats_ is 3 Giant_rat

Item _lure_ large_sack

variable _working_
variable _inside1_
variable _inside2_
variable _inside3_
variable _abandoned_
variable _winchester_
variable _leftrc_

--	Quest start-up:
	start timer _timer_
    hide npc _questgiver_
    add _questgiver_ face
	log 1100 step 0
    pc at _rathouse1_ set _inside1_
    pc at _rathouse2_ set _inside2_
    pc at _rathouse3_ set _inside3_
    pc at _pub_ set _winchester_

_firstjob_ task:
    when _inside1_ and not _destroyed1_ and not _abandoned_
    place foe _rats_ at _rathouse1_
    get item _lure_ saying 1021
    setvar _working_
    drop _questgiver_ face
    log 1101 step 1
    start timer _nest1_

_secondjob_ task:
    when _inside2_ and not _destroyed2_ and not _abandoned_
    place foe _rats_ at _rathouse2_
    get item _lure_ saying 1023
    setvar _working_
    drop _questgiver_ face
    start timer _nest2_

_thirdjob_ task:
    when _inside3_ and not _destroyed3_ and not _abandoned_
    place foe _rats_ at _rathouse3_
    get item _lure_ saying 1024
    setvar _working_
    drop _questgiver_ face
    start timer _nest3_

_drinks_ task:
	when _alldone_ and _winchester_ and not _tavdone_
    place foe _rats_ at _pub_
    drop _questgiver_ face
    say 1028
    say 1032
    get item _lure_ saying 1033
    setvar _working_
    start timer _tavnest_

_1done_ task:
    when _destroyed1_ and not _abandoned_ and _inside1_
    unset _working_
    clear _spawn1_
    remove foe _rats_
    say 1025
    take _lure_ from pc
    add _questgiver_ face

_next1_ task:
    when _destroyed1_ and not _abandoned_ and not _inside1_
    say 1035
    log 1102 step 2

_2done_ task:
    when _destroyed2_ and not _abandoned_ and _inside2_
    unset _working_
    clear _spawn2_
    remove foe _rats_
    say 1026
    take _lure_ from pc
    add _questgiver_ face

_next2_ task:
    when _destroyed2_ and not _abandoned_ and not _inside2_
    say 1036
    log 1103 step 3

_3done_ task:
    when _destroyed3_ and not _abandoned_ and _inside3_
    unset _working_
    clear _spawn3_
    remove foe _rats_
    say 1027
    take _lure_ from pc
    add _questgiver_ face

_next3_ task:
    when _destroyed3_ and not _abandoned_ and not _inside3_
    say 1037
    log 1105 step 4 

_tavdone_ task:
    when _destroyedtav_ and not _abandoned_
    unset _working_
    clear _tavspawn_
    remove foe _rats_
    say 1034
    place npc _questgiver_ at _pub_
    take _lure_ from pc
    clear _qgclicked_

_spawn1_ task:
	when _inside1_ and not _1done_ and not _abandoned_
    create foe _rats_ every 3 minutes indefinitely with 60% success

_spawn2_ task:
	when _inside2_ and not _2done_ and not _abandoned_
    create foe _rats_ every 3 minutes indefinitely with 70% success

_spawn3_ task:
	when _inside3_ and not _3done_ and not _abandoned_
    create foe _rats_ every 3 minutes indefinitely with 80% success

_tavspawn_ task:
	when _winchester_ and not _tavdone_ and not _abandoned_
    create foe _rats_ every 3 minutes indefinitely with 90% success

_inside_ task:
    when _inside1_ or _inside2_ or _inside3_ or _winchester_

_alldone_ task:
    when _destroyed1_ and _destroyed2_ and _destroyed3_

_legit_ task:
    when _working_ and not _inside_
    say 1038
    start timer _leftrc_

_returned_ task:
    when _legit_ and _inside_ and not _coward_
    stop timer _leftrc_
    say 1039
    start task _clearcoward_

_clearcoward_ task:
    clear _legit_

_coward_ task:
    when _leftrc_ and _working_ and not _inside_
    say 1030
    setvar _abandoned_
    stop timer _nest1_
    stop timer _nest2_
    stop timer _nest3_
    stop timer _timer_
    log 1104 step 4 
    start timer _plague_
    create foe _rats_ every 10 minutes indefinitely with 50% success
    unset _inside_

_qgclicked_ task:
	clicked npc _questgiver_ 

_timer_ task:
	end quest saying 1003

_nest1_ task:
    create foe _morerats_ every 2 minutes indefinitely with 60% success msg 1022
    start timer _destroyed1_

_nest2_ task:
    create foe _morerats_ every 2 minutes indefinitely with 75% success msg 1022
    start timer _destroyed2_

_nest3_ task:
    create foe _morerats_ every 2 minutes indefinitely with 90% success msg 1022
    start timer _destroyed3_

_tavnest_ task:
    create foe _morerats_ every 2 minutes indefinitely with 90% success msg 1022
    start timer _destroyedtav_

_pcgetsgold_ task:
    when _qgclicked_ and _tavdone_
    start quest PRA1002b
    say 1029
	give pc _reward_
	end quest 

_destroyed1_ task:
    clear _nest1_

_destroyed2_ task:
    clear _nest2_

_destroyed3_ task:
    clear _nest3_

_destroyedtav_ task:
    clear _tavnest_

_plague_ task:
    end quest saying 1031

_clearclick_ task:
	when _qgclicked_ and not _alldone_
	clear _qgclicked_ _clearclick_ 