Quest: PRA1007
DisplayName: All Mixed Up
-- Message panels
QRC:

QuestorOffer:  [1000]
%oth, you look like that %ra, %pcf. Is that you?
<--->
You're not by any chance %pcf, are you?
<--->
Wait a minute. You're not %pcf, are you?

RefuseQuest:  [1001]
Oh. I guess I was wrong.
<ce>
Sorry.
<--->
Really? Are you sure?
<ce>
I get a little confused sometimes.

AcceptQuest:  [1002]
Thief! Thiiiief! You're the one that took my gem!
Guards! Guaaards!

QuestFail:  [1003]
Real life is not for you. Best find a dungeon to die in.

QuestComplete:  [1004]
_questgiver_ should get help now.

RumorsDuringQuest:  [1005]
I hear _questgiver_ has run off again.
<--->
_questgiver_ has had another one of %g3 turns.
<--->
Apparently some %ra spooked _questgiver_. Some people have no shame.
<--->
Some nutter is hiding under a table in __questgiver_ in ___questgiver_ and they can't get %g2 to come out.

--More Rumors
Message: 1011
A figure was seen sneaking out the back door of __store_'s _store_. Slunk off towards _res_ in __res_.

Message: 1012
I saw _questgiver_ leaving __questgiver_ ___questgiver_ a short while ago. Looking furtive was %g. Seemed to head towards _remtav_ in __remtav_.

Message: 1013
My brother said he saw someone riding hell for leather out of __remtav_, heading towards _dun_.

Message: 1014
Someone was seen going into _dun_. Probably dead now.

--Quest Dialog
Message:  1050
As you hear the accusation, you wonder if perhaps
you did steal %g3 gem? However you have no
recollection of doing so.
<ce>
It might have been another %ra called %pcf...

Message:  1051
_questgiver_ has just scurried out the door.
No guards appear to be coming.
<ce>
You notice a local who glares at you and approaches.

Message:  1052
"%oth! What did you say to %g2?!"
<ce>
"You know what? It doesn't matter! _questgiver_
was doing well until some bloody %ra went and set
%g2 off again."
<ce>
"Well, go on! Go and find %g2 before %g hurts %g2self
or gets lost in a dungeon or something."

Message:  1053
"Try asking around town if anyone has seen %g2,
or they know anything of note."
<ce>
"Just bring %g2 to me at _house_
when you find %g2. Ask for _local0_."
<ce>
"If _questgiver_'s still in an agitated state
you may have to... subdue %g2. I'd say %g probably
needs a bit of a lie down."

Message: 1055
"_questgiver_? Yes, %g was here. Wouldn't leave.
Hid under the table saying a %ra was trying
'inner-act' with %g2. Crazy if you ask me."
<ce>
"Ran off towards __questgiver_ ___questgiver_. Glad to be shot of %g2."
<--->
"Yes. _questgiver_ was here. Hiding in the corner
there."
<ce>
"Was here all night in fact. I offered %g2 some
food but %g just muttered about __questgiver_ ___questgiver_."
<ce>
"You might want to check there."
<--->
"I'm sorry, you've just missed %g. Hid out in
corner. Seemed agitated."
<ce>
"Talked about __questgiver_ ___questgiver_."

Message:  1057
"You still haven't found _questgiver_? As you can
see %g hasn't shown up here."
<ce>
"Try asking if anyone has any news, if they can't
tell you about _questgiver_ directly."

Message:  1059
"No! It's you. What do you want from me? Leave me
alone!"

Message:  1060
"Please help me. I don't know how I got here. I
just want to go home."
<ce>
"_local0_ sent you? Oh thank you. Please take me home!"

Message:  1061
"Glad you brought _questgiver_ back, %pcf.
Can you bring %g2 to __temple_?
I spoke to the Healer, _doctor_ at
_temple_ there and they have a
new treatment they want to try."

Message:  1062
"%oth, you look like that worg, %n. Is that me?"
<--->
"You are by any chance %n, are we?"
<--->
"Wait a minute. You're not %n, we they?"

Message:  1063
"Ah, _questgiver_ I was expecting you.
I hear the potion we gave you has lost it's
effectiveness somewhat."
<ce>
"Not to fear, we have a new one to try. It should
help keep those nasty thoughts at bay."
<ce>
"And thank you, %pcf, for bringing %g2 to us.
We can take it from here."

Message:  1064
"This should cover your expenses."

--Logs
Message:  1150
%qdt:
_questgiver_ of __tav_ suddenly
accused me of theft before calling the guards
and running out of the building screaming.
<ce>
I'm sure I'm probably innocent of this,
but will have to explain that to the guards.
<ce>
If they'll listen.

Message:  1151
%qdt:
No guards came. Instead _local0_
told me _questgiver_ has a few wheels
missing from %g3 cart.
<ce>
Apparently they now expect me to track %g2
down and bring %g2 to them at _house_.
<ce>
I'll need to ask around to figure out where %g
has run off to.

Message:  1152
%qdt:
_questgiver_ was at __questgiver_ but apparently isn't
there anymore. I guess I need to ask around
again, see if anyone has seen %g2.

Message:  1153
%qdt:
_questgiver_ was here at __questgiver_ but has left.
Who knows where. I guess I could ask people.

Message:  1154
%qdt:
Missed %g2, but apparently
_questgiver_ was heard mumbling something
about __questgiver_. I best find them
before they get hurt.

Message:  1170
%qdt:
I found _questgiver_. Had to subdue %g2.
Now to get him back to _local0_
at _house_ in __house_ before he wakes up.

Message:  1156
%qdt:
I found _questgiver_ in _dun_. It appears
that %g has calmed down somewhat and wants to go
home. Now to get out.

Message:  1157
%qdt:
_local0_ has told me to take _questgiver_
to _temple_ in __temple_
where _doctor_ has a new treatment
for %g3 "condition".

--AnyInfo
Message:  1201
I hear _questgiver_ got thrown from a horse while travelling fast and says %g fell through the world and there was nothing there. Just a big void.
<--->
What I'd give to know about _questgiver_... no really. I'm writing a book about %g2. I think %g's an interesting character.
<--->
I talked to _questgiver_ once and %g told me none of us are real.
<--->
I had a good rumour about _questgiver_ but it got left out.
<--->
_questgiver_ has called for the guards so much they no longer get placed near %g2.
<--->
I hear some %ra has been asking questions about _questgiver_. Oh that's you? Sorry. That's all I know.
<--->
They say _questgiver_ is a liar, but if one believes their lies are the truth are they really lies? Something to think about.
<--->
Did _questgiver_ run off again? Sometimes %g hides out in _store_ when %g is having one of %g3 funny turns. Maybe look there.
<--->
I'd avoid _questgiver_ if I were you. Bit of a nutter that one.
<--->
_questgiver_ has some issues. I filed a report but it got closed.
<--->
_questgiver_ once told me that I looked rough because someone called Ninelan forgot about me. What a weird thing to say.
<--->
_questgiver_ has some creepy crawlies inside %g2. So %g says. Crazy talk.
<--->
He goes to _res_ sometimes when he's having a bad day.
<--->
_questgiver_? Good friends with _local4_ I believe.
<--->
Seen him sipping cider at _tav_. Probably shouldn't be when he's taking the tinctures.

Message: 1202
_local0_ is a decent sort. Always looking out for that poor _questgiver_. Lives at _house_ in __house_.

Message:  1250
%pcf
Sorry to have to tell you, but _questgiver_
was found dead in _dun_ by some adventurers
so you can call off your obviously thorough
search of the region.
<ce>
Be careful what you say to people in future, eh?
<ce>
                            _local0_
                            __local0_
                            ____local0_

-- Messages
Message:  1300
A scruffy boy with a runny nose hands you a letter. Stares at you for a bit. Then wanders off.

Message:  1301
Damn. %g's awake again. Quickly knock him out?

Message:  1302
_questgiver_ is unconscious. Time to get him home.

Message: 1303
He's done a runner. Better get after him.

Message: 1304
_questgiver_ appears to almost instantly
vanish before your eyes only to be replaced by
a raving lunatic who attacks desperately.
<ce>
Metaphorically speaking, of course.

Message: 1305
_questgiver_ has passed out again thankfully.

QBN:
Person _questgiver_ group Questor male anyInfo 1201
Person _local0_ group Resident2 anyInfo 1202
Person _local1_ group Resident1
Person _local2_ group Resident2
Person _local3_ group Resident3
Person _local4_ group Resident4
Person _doctor_ group Cleric

Place _store_ local generalstore
Place _res_ local house
Place _res2_ local house
Place _house_ local house
Place _tav_ local tavern
Place _dun_ remote dungeon
Place _temple_ remote temple

Item _deathnotice_ letter used 1250
Item _reward_ gold

Foe _lunatic_ is Warrior

Clock _gone_ 00:01
Clock _fled_ 03:00 1.00:00
Clock _chase_ 00:05 01:00
Clock _awake_ 01:00 12:00
Clock _dead_ 7.00:00 10.00:00
Clock _letter_ 3.00:00 5.00:00

variable _crazy_
variable _hidingstore_
variable _hidingtav_
variable _hidingres_
variable _hidingdun_
variable _atwild_
variable _atstore_
variable _attav_
variable _atres_
variable _atdun_
variable _rescued_
variable _treated_

--	Quest start-up:
    say 1050
    pc at _store_ set _atstore_
    pc at _tav_ set _attav_
    pc at _res_ set _atres_
    pc at _dun_ set _atdun_
    dialog link for location _store_ person _questgiver_
    dialog link for location _res_ person _questgiver_
    dialog link for location _res2_ person _questgiver_
    dialog link for location _tav_ person _questgiver_
    dialog link for location _dun_ person _questgiver_
    log 1150 step 0
    hide npc _questgiver_
    start timer _gone_

_gone_ task:
    play sound door_slam every 0 minutes 1 times
    say 1051
    say 1052
    say 1053
    log 1151 step 1
    place npc _local0_ at _house_
    place npc _local1_ at _store_
    place npc _local2_ at _res_
    place npc _local3_ at _tav_
    place npc _local4_ at _res2_
    start task _fled_

_fled_ task:
    pick one of _tores_ _tostore_ _totav_ _tores2_

_tores_ task:
    restore npc _local1_
    hide npc _local2_
    restore npc _local3_
    restore npc _local4_
    place npc _questgiver_ at _res_
    start timer _fled_

_tores2_ task:
    restore npc _local1_
    restore npc _local2_
    restore npc _local3_
    hide npc _local4_
    place npc _questgiver_ at _res_
    start timer _fled_

_tostore_ task:
    hide npc _local1_
    restore npc _local2_
    restore npc _local3_
    restore npc _local4_
    place npc _questgiver_ at _store_
    start timer _fled_

_totav_ task:
    restore npc _local1_
    restore npc _local2_
    hide npc _local3_
    restore npc _local4_
    place npc _questgiver_ at _tav_
    start timer _fled_

_todun_ task:
    when _tores_ and _totav_ and _tores2_ and _tostore_
    place npc _questgiver_ at _dun_
    start timer _dead_

_panic_ task:
    when _qgclicked_ and not _hidingdun_
    say 1059
    stop timer _fled_
    clear _fled_ _awake_
    hide npc _questgiver_
    pick one of _fight_ _flight_

_fight_ task:
    say 1304
    create foe _lunatic_ every 0 minutes 1 times with 100% success
    clear _qgclicked_

_flight_ task:
    say 1303
    start task _fled_
    clear _qgclicked_

_subdued_ task:
    injured _lunatic_
    say 1302
    remove foe _lunatic_
    add _questgiver_ face
    log 1170 step 4
    start timer _awake_
    clear _panic_ _fight_ _flight_

_awake_ task:
    clear _subdued_
    say 1062
    prompt 1301 yes _gotosleep_ no _panic_

_gotosleep_ task:
    play sound fist_smack_high every 0 minutes 1 times
    say 1305
    setvar _subdued_
    clear _awake_
    start timer _awake_

_clickedlocal1_ task:
    clicked npc _local1_ say 1055

_clickedlocal2_ task:
    clicked npc _local2_ say 1055

_clickedlocal3_ task:
    clicked npc _local3_ say 1055

_clickedlocal4_ task:
    clicked npc _local4_ say 1055

_rescue_ task:
    when _qgclicked_ and _atdun_
    say 1060
    stop timer _dead_
    setvar _rescued_
    add _questgiver_ face
    log 1156 step 5

_friendclicked_ task:
    clicked npc _local0_

_stunned_ task:
    when _subdued_ or _rescued_

_clearfriend_ task:
    when _friendclicked_ and not _stunned_
    say 1057
    clear _friendclicked_ _clearfriend_

_got_ task:
    when _friendclicked_ and _stunned_
    say 1061
    place npc _doctor_ at _temple_
    log 1157 step 6

_doc_ task:
    clicked npc _doctor_

_treatment_ task:
    when _stunned_ and _doc_ 
    say 1063
    say 1064

_dead_ task:
    start timer _letter_

_letter_ task:
    give pc _deathnotice_ notify 1300
    change repute with _local0_ by -5
    end quest

_pcgetsgold_ task:
    when _treatment_
    change repute with _doctor_ by +10
    give pc _reward_
    end quest

_qgclicked_ task:
    clicked npc _questgiver_
